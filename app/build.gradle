apply plugin: 'com.android.application'

def computeVersionCode() {
    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            executable = 'git'
            args = ['log', '--oneline']
            standardOutput = os
        }
        ext.gitRev = os.toString().split('\n').size()
    }
    return gitRev
}

android {
    compileSdkVersion 14
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.rex.lightmeter"
        minSdkVersion 8
        targetSdkVersion 15

        versionName "1.5.0"
        versionCode computeVersionCode()

        testApplicationId "com.rex.lightmeter.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-acra.pro', 'proguard-logback.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:18.0.0'
    compile files('libs/metadata-extractor-2.6.4.jar')

    compile 'ch.acra:acra:4.8.5'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-core:1.1.1-6'
    compile ('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android'
    }
}
